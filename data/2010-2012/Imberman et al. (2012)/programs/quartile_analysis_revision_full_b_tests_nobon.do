***CONDUCTS VARIOUS TESTS USING AN SUR MODEL ACROSS ALL QUARTILES***


* 7.14.10  we switched to using the entire data set...so recode as 0 ( iei non-katrina all students who were never in a Katrina or Rita district)

* 6.24.10 I prepped the TA (test administrator numbers) for 2006-2009...and merge in...test the bad apple model

* 4/22/10  I switched to using scaled scores, try lagged dependent variable on rhs..I've updated the lag to use data back to 2000

* run it with and without lagged dependent
* run it defining percent katrina at school*year and school*year*grade level


* for the lagged dependent variable (math_lag and ela_lag) , I start with the most recent lag.  I then insist that the lag be a pre-katrina score...so in 2007 I allow a 2006 score to be the lag

* for the quartiles of katrina kids performance I use the most recent test score 

* one remaining issue is how I classify non-evacuees into quartiles...I previously used 2000-2005 data which is a little wierd bc those years are also included in the regression

* now I predict math and ela scores and sort into quartiles off predicted values

clear
set mem 8000m
set matsize 8000
capture log close

set seed 10563

*cd "D:\School\Katrina\LA DOE\Revision"
*log using la_log2_quartiles, text replace

* 6.14 try another run in which I use the true lag rather than capping at 2005 
set more off

cd /work/i/imberman/imberman/la_data

use la_prepped_revisionFULL_SAMPLE.dta


drop  gender district_name school_name birth_month birth_day birth_year ela_raw math_raw sci_raw sci_scale scienceachievement soc_raw soc_scale ethnicity00_03 special_ed spec_ed2 school_type home_school ela_numcorrect sci_numcorrect ela_test_status math_test_status sci_test_status soc_test_status ela_achieve math_achieve social_achieve mathMEAN mathSD elaMEAN elaSD neworleans_returning_school neworleans_evacueedistrict new_orleans_area


*MERGE IN ALTERNATIVELY STANDARDIZED ACHIEVEMENT DATA
drop *STD *_lag *_lagyear *0005 math200* ela200*
sort id year
merge id year using alternative_standardization.dta
drop _merge

* fix the quartile analysis...calculate within each year rather than limiting to 2006

capture drop mathQUART elaQUART
capture drop mathQUARTa elaQUARTa

bysort grade_num year: quantiles math0005, gen(mathQUART) nq(4) stable
bysort grade_num year: quantiles ela0005, gen(elaQUART) nq(4) stable

capture drop mathQD*
capture drop elaQD*

tab mathQUART, gen(mathQD)
tab elaQUART, gen(elaQD)



*MAKE GRADE/YEAR RESTRICTIONS

keep if year >= 2000 & year <= 2007
keep if grade_num >= 4 & grade_num <= 10
drop if year == 2006 & grade_num == 4
drop if year == 2007 & grade_num == 4
drop if year == 2007 & grade_num == 5


*IDENTIFY THE LAG SAMPLE
gen lagsample = 0
replace lagsample = 1 if year > 2001 & grade_num == 10
replace lagsample = 1 if year > 2003 & grade_num == 8
replace lagsample = 1 if year > 2005 & (grade_num == 6 | grade_num == 7 | grade_num == 9)



summ mathSTD elaSTD

tab year
*********************************************************************
* drop all the old grade year interactions and use gradenum instead
*********************************************************************
 
*sample 3

drop gryr*


xi i.grade_num*i.year, prefix(gryr)


* make the lags all pre-katrina scores
* it is the most recent lag but always pre-katrina
* save originals as math_lag2 and ela_lag2

gen math_lag2=math_lag
gen ela_lag2=ela_lag
replace math_lag=math0005 if year>2005
replace ela_lag=ela0005 if year>2005


* get baseline score for sorting students
gen math0004=math2004
replace math0004=math2003 if math0004==. & math2003!=.
* replace math0004=math2002 if math0004==. & math2002!=.
* replace math0004=math2001 if math0004==. & math2001!=.
* replace math0004=math2000 if math0004==. & math2000!=.
bysort grade_num year: quantiles math0004 , gen(math_0004QUART) nq(4) stable

gen ela0004=ela2004
replace ela0004=ela2003 if ela0004==. & ela2003!=.
* replace ela0004=ela2002 if ela0004==. & ela2002!=.
* replace ela0004=ela2001 if ela0004==. & ela2001!=.
* replace ela0004=ela2000 if ela0004==. & ela2000!=.
bysort grade_num year: quantiles ela0004 , gen(ela_0004QUART) nq(4) stable





* make 2005 and all its interactions with each grade the ommitted category
*drop gryr*_2005


***********************************
* merge in the discipline data
***********************************
capture drop _m
sort id year

merge id year, using discipline_prepped_microdata

tab _m

drop if _m==2


***********************************
* merge in the test administrator data
***********************************
capture drop _m
sort id year

merge id year, using tanumbers06-09_prepped2.dta

tab _m

drop if _m==2


summ mathSTD elaSTD

tab year



* identify my class using TA numbers

egen class=group(sitecode year elamthtanumber)

***********************************
* recode the discipline data: missing means no discpline record for that student
***********************************
rename discpline_any discipline_any
recode discipline_any .=0

recode  disciplinedaycnt .=0

recode free_lunchA .=0 3=.

* define a dummy for cameron and calcasiu parishes

gen cameron_calcasieu=0
replace cameron_calcasieu=1 if district_code==10 | district_code==12



* redefine katrina_district2 so that cameron and calcasieu districts are also excluded in the peer effects regressions

replace katrina_district2=1 if cameron_calcasieu==1


* define percent Rita

egen percent_ritaTIMESERIES2=mean(rita), by(district_code school_code year)
replace percent_ritaTIMESERIES2=0 if year<=2005


***********************************************
***********************************************

***************************
* redefine percent katrina
****************************
***********************************************
***********************************************
***********************************************

* create katrina_district3 which is a variable for EVER being in a katrina district

egen katrina_district3=max(katrina_district2), by(id)

replace katrina_district3=0 if katrina_district3==.

** do it within school and grade and class 
** include the Rita kids as katrina kids

capture drop percent_katrinaTIMESERIES2

**
replace katrina_sum=1 if rita==1

* make kids with missing katrina_sum 0 if katrina_district3 is 0

replace katrina_sum=0 if katrina_sum==. & katrina_district3==0

egen percent_katrinaTIMESERIES2=mean(katrina_sum), by(district_code school_code year)
replace percent_katrinaTIMESERIES2=0 if year<=2005


egen percent_katrinaTIMESERIESG=mean(katrina_sum), by(district_code school_code year grade_num)
replace percent_katrinaTIMESERIESG=0 if year<=2005




* gen white=(black==0 & asian==0 & hisp==0)

* gen grade_num=real(grade)



***************************
* define katrina peer averages of discplined vs not
****************************

gen discipline05a=discipline_any if year==2005

egen discipline05=max(discipline05a), by(id)

egen percent_katrinaDISCIPLINE2=mean(katrina_sum*discipline05), by(district_code school_code year)
replace percent_katrinaDISCIPLINE2=0 if year<=2005


* count the number of katrina kids in my class
egen count_katrinaDISCIPLINE_class=sum(katrina_sum*discipline05), by(class)
replace count_katrinaDISCIPLINE_class=. if sitecode==. | elamth==.

egen count_katrina_class=sum(katrina_sum), by(class)
replace count_katrina_class=. if sitecode==. | elamth==.






* get fraction of all peers who are in each quartile
egen fraction_mathQ1=mean(mathQD1), by(district_code school_code year)
egen fraction_mathQ2=mean(mathQD2), by(district_code school_code year)
egen fraction_mathQ3=mean(mathQD3), by(district_code school_code year)
egen fraction_mathQ4=mean(mathQD4), by(district_code school_code year)


egen fraction_elaQ1=mean(elaQD1), by(district_code school_code year)
egen fraction_elaQ2=mean(elaQD2), by(district_code school_code year)
egen fraction_elaQ3=mean(elaQD3), by(district_code school_code year)
egen fraction_elaQ4=mean(elaQD4), by(district_code school_code year)





* get fraction of katrina peers who are in each quartile

egen Kfraction_mathQ1=mean(mathQD1*katrina_sum), by(district_code school_code year)
egen Kfraction_mathQ2=mean(mathQD2*katrina_sum), by(district_code school_code year)
egen Kfraction_mathQ3=mean(mathQD3*katrina_sum), by(district_code school_code year)
egen Kfraction_mathQ4=mean(mathQD4*katrina_sum), by(district_code school_code year)


egen Kfraction_elaQ1=mean(elaQD1*katrina_sum), by(district_code school_code year)
egen Kfraction_elaQ2=mean(elaQD2*katrina_sum), by(district_code school_code year)
egen Kfraction_elaQ3=mean(elaQD3*katrina_sum), by(district_code school_code year)
egen Kfraction_elaQ4=mean(elaQD4*katrina_sum), by(district_code school_code year)





replace Kfraction_mathQ1=0 if year<=2005
replace Kfraction_mathQ2=0 if year<=2005
replace Kfraction_mathQ3=0 if year<=2005
replace Kfraction_mathQ4=0 if year<=2005

replace Kfraction_elaQ1=0 if year<=2005
replace Kfraction_elaQ2=0 if year<=2005
replace Kfraction_elaQ3=0 if year<=2005
replace Kfraction_elaQ4=0 if year<=2005

** Now calculate the fraction of katrina peers within each school district code and gradenum

* get fraction of katrina peers who are in each quartile

egen Kfraction_mathQ1G=mean(mathQD1*katrina_sum), by(district_code school_code year grade_num)
egen Kfraction_mathQ2G=mean(mathQD2*katrina_sum), by(district_code school_code year grade_num)
egen Kfraction_mathQ3G=mean(mathQD3*katrina_sum), by(district_code school_code year grade_num)
egen Kfraction_mathQ4G=mean(mathQD4*katrina_sum), by(district_code school_code year grade_num)


egen Kfraction_elaQ1G=mean(elaQD1*katrina_sum), by(district_code school_code year grade_num)
egen Kfraction_elaQ2G=mean(elaQD2*katrina_sum), by(district_code school_code year grade_num)
egen Kfraction_elaQ3G=mean(elaQD3*katrina_sum), by(district_code school_code year grade_num)
egen Kfraction_elaQ4G=mean(elaQD4*katrina_sum), by(district_code school_code year grade_num)



replace Kfraction_mathQ1G=0 if year<=2005
replace Kfraction_mathQ2G=0 if year<=2005
replace Kfraction_mathQ3G=0 if year<=2005
replace Kfraction_mathQ4G=0 if year<=2005

replace Kfraction_elaQ1G=0 if year<=2005
replace Kfraction_elaQ2G=0 if year<=2005
replace Kfraction_elaQ3G=0 if year<=2005
replace Kfraction_elaQ4G=0 if year<=2005





**************
** try interacting lag with number of years between now and lag
**************
gen math_lagyear2=math_lagyear
gen ela_lagyear2=ela_lagyear

replace math_lagyear = . if year > 2005
replace ela_lagyear = . if year > 2005
forvalues year = 2000/2005 {
  replace math_lagyear = `year' if math0005 == math`year' & math_lag != . & year > 2005
  replace ela_lagyear = `year' if ela0005 == ela`year' & ela_lag != . & year > 2005
}

gen year_gap=year-math_lagyear
tab year_gap, gen(year_gapD)


gen year_gapE=year-ela_lagyear
tab year_gapE, gen(year_gapED)



gen math_lag_1=math_lag*year_gapD1
gen math_lag_2=math_lag*year_gapD2
gen math_lag_3=math_lag*year_gapD3
gen math_lag_4=math_lag*year_gapD4
gen math_lag_5=math_lag*year_gapD5
gen math_lag_6=math_lag*year_gapD6
gen math_lag_7=math_lag*year_gapD7



gen ela_lag_1=ela_lag*year_gapED1
gen ela_lag_2=ela_lag*year_gapED2
gen ela_lag_3=ela_lag*year_gapED3
gen ela_lag_4=ela_lag*year_gapED4
gen ela_lag_5=ela_lag*year_gapED5
gen ela_lag_6=ela_lag*year_gapED6
gen ela_lag_7=ela_lag*year_gapED7



*RESTRICT TO NON-EVACUEES IN SCHOOLS THAT WERE NOT IN EVAC AREA
keep if katrina_sum != 1 & katrina_district2==0 & percent_katrina<.7



**************
** Overall Table ***
**************

*LIMIT TO LAG SAMPLE
keep if lagsample == 1

**SUR CANNOT BE DONE WITH AREG SO NEED TO USE DUMMY VARIABLES
xi i.sitecode

****************************
** BY NATIVE LAGGED SCORE QUARTILE**
****************************


forvalues quart = 1/4 {

di ""
di "****************"
di "QUARTILE `quart'"
di "****************"
di ""


*LAG SAMPLE (WITH LAGS - 2002 & LATER, MIDHIGH ONLY)


reg mathSTD math_lag_* Kfraction_mathQ1G Kfraction_mathQ2G Kfraction_mathQ3G Kfraction_mathQ4G free_lunchA male black hisp asian gryr* _I* if mathQD`quart' == 1
estimates store math`quart'


reg elaSTD ela_lag_* Kfraction_elaQ1G Kfraction_elaQ2G Kfraction_elaQ3G Kfraction_elaQ4G free_lunchA male black hisp asian gryr* _I* if elaQD`quart' == 1
estimates store ela`quart'

}

  # delimit ;

*MATH TESTS;
suest math1 math2 math3 math4, vce(cluster sitecode);




  *DO BONFERRINI CORRECTIONS FOR ALL SINGLE COEFFICIENT ESTIMATES;
  test 	[math1_mean]Kfraction_mathQ1G [math1_mean]Kfraction_mathQ2G [math1_mean]Kfraction_mathQ3G [math1_mean]Kfraction_mathQ4G
	[math2_mean]Kfraction_mathQ1G [math2_mean]Kfraction_mathQ2G [math2_mean]Kfraction_mathQ3G [math2_mean]Kfraction_mathQ4G
	[math3_mean]Kfraction_mathQ1G [math3_mean]Kfraction_mathQ2G [math3_mean]Kfraction_mathQ3G [math3_mean]Kfraction_mathQ4G
	[math4_mean]Kfraction_mathQ1G [math4_mean]Kfraction_mathQ2G [math4_mean]Kfraction_mathQ3G [math4_mean]Kfraction_mathQ4G 
	, mtest;

  *WEAK MONOTONICITY ;
  test 	([math1_mean]Kfraction_mathQ1G = [math1_mean]Kfraction_mathQ4G)  
	([math2_mean]Kfraction_mathQ1G = [math2_mean]Kfraction_mathQ4G)	/*FOR MONOTONICITY THESE SHOUDL BE POSITIVE */
	([math3_mean]Kfraction_mathQ1G = [math3_mean]Kfraction_mathQ4G)
	([math4_mean]Kfraction_mathQ1G = [math4_mean]Kfraction_mathQ4G)
	, mtest;




  *STRONG MONOTONICITY 1;
  test 	([math1_mean]Kfraction_mathQ1G = [math1_mean]Kfraction_mathQ2G)	/*FOR MONOTONICITY THESE SHOULD BE POSITIVE */  
	([math1_mean]Kfraction_mathQ2G = [math1_mean]Kfraction_mathQ3G)
	([math1_mean]Kfraction_mathQ3G = [math1_mean]Kfraction_mathQ4G)
	([math2_mean]Kfraction_mathQ1G = [math2_mean]Kfraction_mathQ2G)  
	([math2_mean]Kfraction_mathQ2G = [math2_mean]Kfraction_mathQ3G)
	([math2_mean]Kfraction_mathQ3G = [math2_mean]Kfraction_mathQ4G)
	([math3_mean]Kfraction_mathQ1G = [math3_mean]Kfraction_mathQ2G)  
	([math3_mean]Kfraction_mathQ2G = [math3_mean]Kfraction_mathQ3G)
	([math3_mean]Kfraction_mathQ3G = [math3_mean]Kfraction_mathQ4G)
	([math4_mean]Kfraction_mathQ1G = [math4_mean]Kfraction_mathQ2G)  
	([math4_mean]Kfraction_mathQ2G = [math4_mean]Kfraction_mathQ3G)
	([math4_mean]Kfraction_mathQ3G = [math4_mean]Kfraction_mathQ4G)
	, mtest;


  *STRONG MONOTONICITY 2;
  test 	([math1_mean]Kfraction_mathQ1G = [math1_mean]Kfraction_mathQ2G = [math1_mean]Kfraction_mathQ3G = [math1_mean]Kfraction_mathQ4G);
  test	([math2_mean]Kfraction_mathQ1G = [math2_mean]Kfraction_mathQ2G = [math2_mean]Kfraction_mathQ3G = [math2_mean]Kfraction_mathQ4G);
  test	([math3_mean]Kfraction_mathQ1G = [math3_mean]Kfraction_mathQ2G = [math3_mean]Kfraction_mathQ3G = [math3_mean]Kfraction_mathQ4G);
  test	([math4_mean]Kfraction_mathQ1G = [math4_mean]Kfraction_mathQ2G = [math4_mean]Kfraction_mathQ3G = [math4_mean]Kfraction_mathQ4G);



  *INVIDIOUS COMPARISON - HIGHER ACHIEVERS WORSE FOR YOU & LOWER ACHIEVERS BETTER FOR YOU;

  test 	 [math1_mean]Kfraction_mathQ2G  /* THESE SHOULD BE POSITIVE FOR IC */
	 [math1_mean]Kfraction_mathQ3G  
	 [math1_mean]Kfraction_mathQ4G
	 [math2_mean]Kfraction_mathQ3G
	 [math2_mean]Kfraction_mathQ4G
	 [math3_mean]Kfraction_mathQ4G
	 [math2_mean]Kfraction_mathQ1G	/* THESE SHOULD BE NEGATIVE FOR IC */
	 [math3_mean]Kfraction_mathQ1G
	 [math3_mean]Kfraction_mathQ2G
	 [math4_mean]Kfraction_mathQ1G
	 [math4_mean]Kfraction_mathQ2G
	 [math4_mean]Kfraction_mathQ3G
	, mtest;

  *ABILITY GROUPING (BOUTIQUE/TRACKING) - YOUR OWN QUARTILE BEATS HIGHER QUARTILES;
  test 	([math1_mean]Kfraction_mathQ1G = [math1_mean]Kfraction_mathQ2G) /* THESE SHOULD BE POSITIVE FOR AG */
 	([math1_mean]Kfraction_mathQ1G = [math1_mean]Kfraction_mathQ3G)
	([math1_mean]Kfraction_mathQ1G = [math1_mean]Kfraction_mathQ4G)
	([math2_mean]Kfraction_mathQ2G = [math2_mean]Kfraction_mathQ3G)
	([math2_mean]Kfraction_mathQ2G = [math2_mean]Kfraction_mathQ4G)
	([math3_mean]Kfraction_mathQ3G = [math3_mean]Kfraction_mathQ4G)
	, mtest;

   *DUFLO, ET. AL.  --> PROP 1 - TEST IF BELOW MEDIAN NATIVES HAVE NEGATIVE IMPACTS FROM BEING COMBINED W/ OWN QUARTILE WHILE ABOVE MEDIAN;
   *HAVE POSITIVE IMPACTS FROM BEING COMBINED W/ OWN QUARTILE;
   test [math1_mean]Kfraction_mathQ1G 
	[math2_mean]Kfraction_mathQ2G 
	[math3_mean]Kfraction_mathQ3G
	[math4_mean]Kfraction_mathQ4G
	, mtest;


   *DUFLO, ET. AL. --> PROP 3 - 
	f() INCREASING IN PEER ACHIEVEMENT IF (Q_NATIVE_KAT), Q4G4 > 0, Q14 > 0, & Q4G4 > Q14 AND Q11 < 0, Q4G1 < 0, & Q11 > Q4G1
		NULL IS THAT f() INCREASING
		REJECT NULL IF
			Q4G4 < 0
			Q14 < 0
			Q11 > 0
			Q4G1 > 0
			Q4G4 < Q14 or
			Q11 < Q4G1
	NOTE THAT THIS IS A STRONGER TEST THAN DUFLO ET. AL. MIGHT SUGGEST AS IN THIER CONTEXT REJECT IF Q4G4 <= Q4G1 OR Q11 <= Q14;

   test ([math4_mean]Kfraction_mathQ4G)	/*  NEGATIVE = REJECT */
 	([math1_mean]Kfraction_mathQ4G)
	([math4_mean]Kfraction_mathQ1G)	/* POSITIVE = REJECT */
	([math1_mean]Kfraction_mathQ1G)
	([math4_mean]Kfraction_mathQ4G = [math1_mean]Kfraction_mathQ4G) /* NEGATIVE = REJECT */
	([math1_mean]Kfraction_mathQ1G = [math4_mean]Kfraction_mathQ1G)
	, mtest;

	*IF f() CONSTANT THEN INCREASE (DECREASE) IN PEER SCORES
		(1) INCREASE (DECREASE) ACHIEVEMENT IN ABOVE MEDIAN
		(2) DECREASE (INCREASE) ACHIEVEMENT JUST BELOW MEDIAN
		(3) NO EFFECT FAR BELOW MEDIAN
		HENCE REJECT IF

			Q4G4 < 0		(1)
			Q3G4 < 0
			Q4G1 > 0
			Q3G1 > 0
			
	 		Q2G4 > 0		(2)
			Q2G1 < 0

			Q14 > 0		(3)
		OR	Q14 < 0	
	;

  test 	[math4_mean]Kfraction_mathQ4G	/* NEGATIVE = REJECT */
	[math3_mean]Kfraction_mathQ4G
	[math2_mean]Kfraction_mathQ1G

	[math4_mean]Kfraction_mathQ1G	/* POSITIVE = REJECT */
	[math3_mean]Kfraction_mathQ1G
	[math2_mean]Kfraction_mathQ4G

	[math1_mean]Kfraction_mathQ4G	/* REJECT IF NOT = 0 */
	, mtest;



*ELA TESTS;
suest ela1 ela2 ela3 ela4, vce(cluster sitecode);


  *DO ALL SINGLE COEFFICIENT ESTIMATES;
  test 	[ela1_mean]Kfraction_elaQ1G [ela1_mean]Kfraction_elaQ2G [ela1_mean]Kfraction_elaQ3G [ela1_mean]Kfraction_elaQ4G
	[ela2_mean]Kfraction_elaQ1G [ela2_mean]Kfraction_elaQ2G [ela2_mean]Kfraction_elaQ3G [ela2_mean]Kfraction_elaQ4G
	[ela3_mean]Kfraction_elaQ1G [ela3_mean]Kfraction_elaQ2G [ela3_mean]Kfraction_elaQ3G [ela3_mean]Kfraction_elaQ4G
	[ela4_mean]Kfraction_elaQ1G [ela4_mean]Kfraction_elaQ2G [ela4_mean]Kfraction_elaQ3G [ela4_mean]Kfraction_elaQ4G 
	, mtest;

  *WEAK MONOTONICITY;
  test 	([ela1_mean]Kfraction_elaQ1G = [ela1_mean]Kfraction_elaQ4G)  
	([ela2_mean]Kfraction_elaQ1G = [ela2_mean]Kfraction_elaQ4G)	/*FOR MONOTONICITY THESE SHOULD BE POSITIVE */
	([ela3_mean]Kfraction_elaQ1G = [ela3_mean]Kfraction_elaQ4G)
	([ela4_mean]Kfraction_elaQ1G = [ela4_mean]Kfraction_elaQ4G)
	, mtest;


  *STRONG MONOTONICITY 1;
  test 	([ela1_mean]Kfraction_elaQ1G = [ela1_mean]Kfraction_elaQ2G)	/*FOR MONOTONICITY THESE SHOULD BE POSITIVE */  
	([ela1_mean]Kfraction_elaQ2G = [ela1_mean]Kfraction_elaQ3G)
	([ela1_mean]Kfraction_elaQ3G = [ela1_mean]Kfraction_elaQ4G)
	([ela2_mean]Kfraction_elaQ1G = [ela2_mean]Kfraction_elaQ2G)  
	([ela2_mean]Kfraction_elaQ2G = [ela2_mean]Kfraction_elaQ3G)
	([ela2_mean]Kfraction_elaQ3G = [ela2_mean]Kfraction_elaQ4G)
	([ela3_mean]Kfraction_elaQ1G = [ela3_mean]Kfraction_elaQ2G)  
	([ela3_mean]Kfraction_elaQ2G = [ela3_mean]Kfraction_elaQ3G)
	([ela3_mean]Kfraction_elaQ3G = [ela3_mean]Kfraction_elaQ4G)
	([ela4_mean]Kfraction_elaQ1G = [ela4_mean]Kfraction_elaQ2G)  
	([ela4_mean]Kfraction_elaQ2G = [ela4_mean]Kfraction_elaQ3G)
	([ela4_mean]Kfraction_elaQ3G = [ela4_mean]Kfraction_elaQ4G)
	, mtest;



  *STRONG MONOTONICITY 2;
  test 	([ela1_mean]Kfraction_elaQ1G = [ela1_mean]Kfraction_elaQ2G = [ela1_mean]Kfraction_elaQ3G = [ela1_mean]Kfraction_elaQ4G);
  test	([ela2_mean]Kfraction_elaQ1G = [ela2_mean]Kfraction_elaQ2G = [ela2_mean]Kfraction_elaQ3G = [ela2_mean]Kfraction_elaQ4G);
  test	([ela3_mean]Kfraction_elaQ1G = [ela3_mean]Kfraction_elaQ2G = [ela3_mean]Kfraction_elaQ3G = [ela3_mean]Kfraction_elaQ4G);
  test	([ela4_mean]Kfraction_elaQ1G = [ela4_mean]Kfraction_elaQ2G = [ela4_mean]Kfraction_elaQ3G = [ela4_mean]Kfraction_elaQ4G);


  *INVIDIOUS COMPARISON - HIGHER ACHIEVERS WORSE FOR YOU & LOWER ACHIEVERS BETTER FOR YOU;
  test 	 [ela1_mean]Kfraction_elaQ2G  /* THESE SHOULD BE POSITIVE FOR IC */
	 [ela1_mean]Kfraction_elaQ3G  
	 [ela1_mean]Kfraction_elaQ4G
	 [ela2_mean]Kfraction_elaQ3G
	 [ela2_mean]Kfraction_elaQ4G
	 [ela3_mean]Kfraction_elaQ4G
	 [ela2_mean]Kfraction_elaQ1G	/* THESE SHOULD BE NEGATIVE FOR IC */
	 [ela3_mean]Kfraction_elaQ1G
	 [ela3_mean]Kfraction_elaQ2G
	 [ela4_mean]Kfraction_elaQ1G
	 [ela4_mean]Kfraction_elaQ2G
	 [ela4_mean]Kfraction_elaQ3G
	, mtest;

  *ABILITY GROUPING (BOUTIQUE/TRACKING) - YOUR OWN QUARTILE BEATS HIGHER QUARTILES;
  test 	([ela1_mean]Kfraction_elaQ1G = [ela1_mean]Kfraction_elaQ2G) /* THESE SHOULD BE POSITIVE FOR AG */
 	([ela1_mean]Kfraction_elaQ1G = [ela1_mean]Kfraction_elaQ3G)
	([ela1_mean]Kfraction_elaQ1G = [ela1_mean]Kfraction_elaQ4G)
	([ela2_mean]Kfraction_elaQ2G = [ela2_mean]Kfraction_elaQ3G)
	([ela2_mean]Kfraction_elaQ2G = [ela2_mean]Kfraction_elaQ4G)
	([ela3_mean]Kfraction_elaQ3G = [ela3_mean]Kfraction_elaQ4G)
	, mtest;

   *DUFLO, ET. AL.  --> PROP 1 - TEST IF BELOW MEDIAN NATIVES HAVE NEGATIVE IMPACTS FROM BEING COMBINED W/ OWN QUARTILE WHILE ABOVE MEDIAN;
   *HAVE POSITIVE IMPACTS FROM BEING COMBINED W/ OWN QUARTILE;
   test [ela1_mean]Kfraction_elaQ1G 
	[ela2_mean]Kfraction_elaQ2G 
	[ela3_mean]Kfraction_elaQ3G
	[ela4_mean]Kfraction_elaQ4G
	, mtest;



   *DUFLO, ET. AL. --> PROP 3 - 
	f() INCREASING IN PEER ACHIEVEMENT IF (Q_NATIVE_KAT), Q4G4 > 0, Q14 > 0, & Q4G4 > Q14 AND Q11 < 0, Q4G1 < 0, & Q11 > Q4G1
		NULL IS THAT f() INCREASING
		REJECT NULL IF
			Q4G4 < 0
			Q14 < 0
			Q11 > 0
			Q4G1 > 0
			Q4G4 < Q14 or
			Q11 < Q4G1
	NOTE THAT THIS IS A STRONGER TEST THAN DUFLO ET. AL. MIGHT SUGGEST AS IN THIER CONTEXT REJECT IF Q4G4 <= Q4G1 OR Q11 <= Q14;

   test ([ela4_mean]Kfraction_elaQ4G)	/*  CONSISTENT - POSITIVE */
 	([ela1_mean]Kfraction_elaQ4G)

	([ela4_mean]Kfraction_elaQ1G)	/* CONSISTENT - NEGATIVE */
	([ela1_mean]Kfraction_elaQ1G)

	([ela4_mean]Kfraction_elaQ4G = [ela1_mean]Kfraction_elaQ4G) /* CONSISTENT - POSITIVE*/
	([ela1_mean]Kfraction_elaQ1G = [ela4_mean]Kfraction_elaQ1G)
	, mtest;

	*IF f() CONSTANT THEN INCREASE (DECREASE) IN PEER SCORES
		(1) INCREASE (DECREASE) ACHIEVEMENT IN ABOVE MEDIAN
		(2) DECREASE (INCREASE) ACHIEVEMENT JUST BELOW MEDIAN
		(3) NO EFFECT FAR BELOW MEDIAN
		HENCE REJECT IF

			Q4G4 < 0		(1)
			Q34 < 0
			Q4G1 > 0
			Q31 > 0
			
	 		Q24 > 0		(2)
			Q21 < 0

			Q14 > 0		(3)
		OR	Q14 < 0	
	;

  test 	[ela4_mean]Kfraction_elaQ4G	/* CONSISTENT - POSITIVE */
	[ela3_mean]Kfraction_elaQ4G
	[ela2_mean]Kfraction_elaQ1G

	[ela4_mean]Kfraction_elaQ1G	/* CONSISTENT - NEGATIVE */
	[ela3_mean]Kfraction_elaQ1G
	[ela2_mean]Kfraction_elaQ4G

	[ela1_mean]Kfraction_elaQ4G	/* CONSISTENT = 0 */
	, mtest;


